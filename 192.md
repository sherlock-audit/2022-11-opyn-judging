hyh

medium

# withdrawAuction can distribute extra USDC to withdrawer queue

## Summary

withdrawAuction() doesn't check that the needed amount of squeeth is obtained from the orders supplied. If the amount isn't reached, the squeeth from the balance will be used instead.

## Vulnerability Detail

There is no check in withdrawAuction() that `toPull` is zero, which allow for the situation when `ICrabStrategyV2(crab).getWsqueethFromCrabAmount(_p.crabToWithdraw)` requested will not be gathered from the orders supplied to the call.

If there are no squeeth on the balance the function will revert on `ICrabStrategyV2(crab).withdraw(_p.crabToWithdraw)` call.

If there were leftover squeeth funds on the CrabNetting's balance those funds will be send over to the withdrawers queued.

## Impact

Squeeth funds from CrabNetting's balance will be distributed to the current withdrawers queue in USDC form. That's a violation of protocol logic (USDC distributed should be determined by the price and stake), and is a loss of funds for the protocol.

Setting the severity to medium due to prerequisite of squeeth shortage in the provided orders.

## Code Snippet

withdrawAuction() will proceed with `ICrabStrategyV2(crab).withdraw(_p.crabToWithdraw)` even when `toPull > 0`:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L624-L657

```solidity
    /**
     * @dev takes in orders from mm's to sell sqth and withdraws the crab amount in q
     * @param _p Withdraw Params that contain orders, crabToWithdraw, uniswap min amount and fee
     */
    function withdrawAuction(WithdrawAuctionParams calldata _p) public onlyOwner {
        _checkOTCPrice(_p.clearingPrice, true);
        uint256 initWethBalance = IERC20(weth).balanceOf(address(this));
        uint256 initEthBalance = address(this).balance;
        /**
         *     step 1: get sqth from mms
         *     step 2: withdraw from crab
         *     step 3: send eth to mms
         *     step 4: convert eth to usdc
         *     step 5: send usdc to withdrawers
         */

        // step 1 get sqth from mms
        uint256 sqthRequired = ICrabStrategyV2(crab).getWsqueethFromCrabAmount(_p.crabToWithdraw);
        uint256 toPull = sqthRequired;
        for (uint256 i = 0; i < _p.orders.length && toPull > 0; i++) {
            _checkOrder(_p.orders[i]);
            require(!_p.orders[i].isBuying, "auction order is not selling");
            require(_p.orders[i].price <= _p.clearingPrice, "sell order price greater than clearing");
            if (_p.orders[i].quantity < toPull) {
                toPull -= _p.orders[i].quantity;
                IERC20(sqth).transferFrom(_p.orders[i].trader, address(this), _p.orders[i].quantity);
            } else {
                IERC20(sqth).transferFrom(_p.orders[i].trader, address(this), toPull);
                toPull = 0;
            }
        }

        // step 2 withdraw from crab
        ICrabStrategyV2(crab).withdraw(_p.crabToWithdraw);
```


If there are leftover squeeths on the balance, the `ICrabStrategyV2(crab).withdraw(_p.crabToWithdraw)` will not be reverted.

withdrawAuction() will then distribute extra proceeds from those squeeths, that were utilized by withdraw(), to the withdrawers in the USDC form:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L675-L718

```solidity
        ISwapRouter.ExactInputSingleParams memory params = ISwapRouter.ExactInputSingleParams({
            tokenIn: address(weth),
            tokenOut: address(usdc),
            fee: _p.ethUSDFee,
            recipient: address(this),
            deadline: block.timestamp,
            amountIn: (IERC20(weth).balanceOf(address(this)) - initWethBalance),
            amountOutMinimum: _p.minUSDC,
            sqrtPriceLimitX96: 0
        });
        uint256 usdcReceived = swapRouter.exactInputSingle(params);

        // step 5 pay all withdrawers and mark their withdraws as done
        uint256 remainingWithdraws = _p.crabToWithdraw;
        uint256 j = withdrawsIndex;
        uint256 usdcAmount;
        while (remainingWithdraws > 0) {
            Receipt memory withdraw = withdraws[j];
            if (withdraw.amount == 0) {
                j++;
                continue;
            }
            if (withdraw.amount <= remainingWithdraws) {
                // full usage
                remainingWithdraws -= withdraw.amount;
                crabBalance[withdraw.sender] -= withdraw.amount;

                // send proportional usdc
                usdcAmount = (((withdraw.amount * 1e18) / _p.crabToWithdraw) * usdcReceived) / 1e18;
                IERC20(usdc).transfer(withdraw.sender, usdcAmount);
                emit CrabWithdrawn(withdraw.sender, withdraw.amount, usdcAmount, j);
                delete withdraws[j];
                j++;
            } else {
                withdraws[j].amount -= remainingWithdraws;
                crabBalance[withdraw.sender] -= remainingWithdraws;

                // send proportional usdc
                usdcAmount = (((remainingWithdraws * 1e18) / _p.crabToWithdraw) * usdcReceived) / 1e18;
                IERC20(usdc).transfer(withdraw.sender, usdcAmount);
                emit CrabWithdrawn(withdraw.sender, remainingWithdraws, usdcAmount, j);

                remainingWithdraws = 0;
            }
```

The issue here is that `usdcReceived` will correspond to the `(IERC20(weth).balanceOf(address(this)) - initWethBalance)`, which will be withdraw() proceeds minus what was distributed to traders:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L656-L672

```solidity
        // step 2 withdraw from crab
        ICrabStrategyV2(crab).withdraw(_p.crabToWithdraw);

        // step 3 pay all mms
        IWETH(weth).deposit{value: address(this).balance - initEthBalance}();
        toPull = sqthRequired;
        uint256 sqthQuantity;
        for (uint256 i = 0; i < _p.orders.length && toPull > 0; i++) {
            if (_p.orders[i].quantity < toPull) {
                sqthQuantity = _p.orders[i].quantity;
            } else {
                sqthQuantity = toPull;
            }
            IERC20(weth).transfer(_p.orders[i].trader, (sqthQuantity * _p.clearingPrice) / 1e18);
            toPull -= sqthQuantity;
            emit BidTraded(_p.orders[i].bidId, _p.orders[i].trader, sqthQuantity, _p.clearingPrice, false);
        }
```

As lower WETH amount was distributed to the traders, as not enough squeeths were obtained from them in the first place, the WETH that appeared on the balances as the net result will be excessive.

I.e. full WETH amount was obtained from `ICrabStrategyV2(crab).withdraw(_p.crabToWithdraw)`, but less was distributed to the traders, the remainder is excessive.

The `usdcReceived` corresponds to the whole remainder and will be excessive as well. This way extra funds will be returned to withdrawers according to `usdcAmount = (((withdraw.amount * 1e18) / _p.crabToWithdraw) * usdcReceived) / 1e18` as `usdcReceived` will not correspond to `_p.crabToWithdraw`. `usdcReceived` will have extra component from squeeth funds sold from the CrabNetting's balance.

## Tool used

Manual Review

## Recommendation

Consider adding the check for `toPull ==0`:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L624-L657

```solidity
    /**
     * @dev takes in orders from mm's to sell sqth and withdraws the crab amount in q
     * @param _p Withdraw Params that contain orders, crabToWithdraw, uniswap min amount and fee
     */
    function withdrawAuction(WithdrawAuctionParams calldata _p) public onlyOwner {
        _checkOTCPrice(_p.clearingPrice, true);
        uint256 initWethBalance = IERC20(weth).balanceOf(address(this));
        uint256 initEthBalance = address(this).balance;
        /**
         *     step 1: get sqth from mms
         *     step 2: withdraw from crab
         *     step 3: send eth to mms
         *     step 4: convert eth to usdc
         *     step 5: send usdc to withdrawers
         */

        // step 1 get sqth from mms
        uint256 sqthRequired = ICrabStrategyV2(crab).getWsqueethFromCrabAmount(_p.crabToWithdraw);
        uint256 toPull = sqthRequired;
        for (uint256 i = 0; i < _p.orders.length && toPull > 0; i++) {
            _checkOrder(_p.orders[i]);
            require(!_p.orders[i].isBuying, "auction order is not selling");
            require(_p.orders[i].price <= _p.clearingPrice, "sell order price greater than clearing");
            if (_p.orders[i].quantity < toPull) {
                toPull -= _p.orders[i].quantity;
                IERC20(sqth).transferFrom(_p.orders[i].trader, address(this), _p.orders[i].quantity);
            } else {
                IERC20(sqth).transferFrom(_p.orders[i].trader, address(this), toPull);
                toPull = 0;
            }
        }
+       require(toPull == 0, "not enough sell orders for sqth");

        // step 2 withdraw from crab
        ICrabStrategyV2(crab).withdraw(_p.crabToWithdraw);
```