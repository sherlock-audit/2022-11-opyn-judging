hansfriese

medium

# Funds are locked permanently in the CrabNetting contract

## Summary

In the function `depositAuction` and `withdrawAuction`, the funds are calculated by rounding down and this leaves minor amount of funds (ETH/CRAB/USDC). These are locked in the contract permanently.

## Vulnerability Detail

If we look at the function `depositAuction` of CrabNetting.sol#L584-#L618, CRAB and ETH are sent proportionally to the user's deposit amount.
Because every actual amount is calculated by rounding down, it is possible that not all funds are sent. (Even the ETH amount less than 1e12 are not handled.)
I note that these are permanently locked in the contract because the protocol always operates based on before/after balance difference.

```solidity
// CrabNetting.sol #572
to_send.crab = IERC20(crab).balanceOf(address(this)) - initCrabBalance;//@audit-info crab got from two deposits: normal + flash deposit
// get the balance between start and now
to_send.eth = address(this).balance - initEthBalance;
IWETH(weth).deposit{value: to_send.eth}();

while (remainingDeposits > 0) {
    uint256 queuedAmount = deposits[k].amount;
    Portion memory portion;
    if (queuedAmount == 0) {
        k++;
        continue;
    }
    if (queuedAmount <= remainingDeposits) {
        remainingDeposits = remainingDeposits - queuedAmount;
        usdBalance[deposits[k].sender] -= queuedAmount;

        portion.crab = (((queuedAmount * 1e18) / _p.depositsQueued) * to_send.crab) / 1e18;

        IERC20(crab).transfer(deposits[k].sender, portion.crab);

        portion.eth = (((queuedAmount * 1e18) / _p.depositsQueued) * to_send.eth) / 1e18;
        if (portion.eth > 1e12) {
            IWETH(weth).transfer(deposits[k].sender, portion.eth);
        } else {
            portion.eth = 0;
        }
        emit USDCDeposited(deposits[k].sender, queuedAmount, portion.crab, k, portion.eth);

        delete deposits[k];
        k++;
    } else {
        usdBalance[deposits[k].sender] -= remainingDeposits;

        portion.crab = (((remainingDeposits * 1e18) / _p.depositsQueued) * to_send.crab) / 1e18;
        IERC20(crab).transfer(deposits[k].sender, portion.crab);

        portion.eth = (((remainingDeposits * 1e18) / _p.depositsQueued) * to_send.eth) / 1e18;
        if (portion.eth > 1e12) {
            IWETH(weth).transfer(deposits[k].sender, portion.eth);
        } else {
            portion.eth = 0;
        }
        emit USDCDeposited(deposits[k].sender, remainingDeposits, portion.crab, k, portion.eth);

        deposits[k].amount -= remainingDeposits;
        remainingDeposits = 0;
    }
}
```

I know the protocol team is aware of this but does not care because the amount is quite small and does not want to add any kinds of admin withdrawal mechanism.
But for this kind of operation, it is recommended sending the whole left amount to the last receiver (when `queuedAmount >= remainingDeposits` holds) and I think it will give more reputation to this protocol.

## Impact

Minor funds are permanently locked in the contract while it is possible to avoid.

## Code Snippet

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L584-L618
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L688-L718

## Tool used

Manual Review

## Recommendation

Consider sending the whole remaining balance to the last depositor (withdrawer).