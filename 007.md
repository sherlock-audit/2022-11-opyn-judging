zimu

high

# `userDepositsIndex` and `userWithdrawsIndex` are not updated when dequeuing

## Summary
When withdrawing USDC or Crab through dequeuing, `userDepositsIndex` and `userWithdrawsIndex` are not synchronously updated with the queue `deposits` and `withdraws`. That can produce large amount of bad debts, and may delete elements of `deposits` and `withdraws` out of index bound.

## Vulnerability Detail
In function `withdrawUSDC`,  `userDepositsIndex` is not synchronously updated when deleting elements from `deposits`. Actually, when `delete deposits[userDepositsIndex[msg.sender][i - 1]]`, `userDepositsIndex` also needs to delete (i-1)th element from `userDepositsIndex[msg.sender]`, and subs the value of those indexes in `userDepositsIndex` by 1 when their value is in (userDepositsIndex[msg.sender][i - 1], deposits.length-1].

```solidity
    function withdrawUSDC(uint256 _amount) external {
        require(!isAuctionLive, "auction is live");

        usdBalance[msg.sender] = usdBalance[msg.sender] - _amount;
        require(
            usdBalance[msg.sender] >= minUSDCAmount || usdBalance[msg.sender] == 0,
            "remaining amount smaller than minimum, consider removing full balance"
        );

        // start withdrawing from the users last deposit
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userDepositsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = deposits[userDepositsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete deposits[userDepositsIndex[msg.sender][i - 1]];
            }
        }
        IERC20(usdc).transfer(msg.sender, _amount);

        emit USDCDeQueued(msg.sender, _amount, usdBalance[msg.sender]);
    }
```

In function `dequeueCrab`, the problem is the same as in function `withdrawUSDC`.
```solidity
    function dequeueCrab(uint256 _amount) external {
        require(!isAuctionLive, "auction is live");
        crabBalance[msg.sender] = crabBalance[msg.sender] - _amount;
        require(
            crabBalance[msg.sender] >= minCrabAmount || crabBalance[msg.sender] == 0,
            "remaining amount smaller than minimum, consider removing full balance"
        );
        // deQueue crab from the last, last in first out
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userWithdrawsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = withdraws[userWithdrawsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete withdraws[userWithdrawsIndex[msg.sender][i - 1]];
            }
        }
        IERC20(crab).transfer(msg.sender, _amount);
        emit CrabDeQueued(msg.sender, _amount, crabBalance[msg.sender]);
    }
```

## Impact
That can produce large amount of bad debts, and may delete elements of `deposits` and `withdraws` out of index bound.

## Code Snippet
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L278-L304
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L323-L346

## Tool used
Manual Review

## Recommendation
1.  When `delete deposits[userDepositsIndex[msg.sender][i - 1]]`, `userDepositsIndex` also needs to delete (i-1)th element from `userDepositsIndex[msg.sender]`, and subs the value of those indexes in `userDepositsIndex` by 1 when their value is in (userDepositsIndex[msg.sender][i - 1], deposits.length-1] since dequeuing is executed. (The same for `userWithdrawsIndex` and  `withdraws`)
2.  Or to simplify,  change from
```solidity
mapping(address => uint256[]) public userDepositsIndex;
mapping(address => uint256[]) public userWithdrawsIndex;
```
to 
```solidity
mapping(address => uint256) public userDepositsIndex;
mapping(address => uint256) public userWithdrawsIndex;
```
without using arrays, and rewrite codes accordingly.