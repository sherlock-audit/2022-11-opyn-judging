indijanc

high

# An attacker can permanently render the contract useless

## Summary

An attacker can create a large number of empty deposits (and similarly empty withdraws) by many deposits into and withdrawals from the queue. This can leave the contract in a state where `depositAuction()`, `withdrawAuction()` and `netAtPrice()` always fails with out of gas error.

## Vulnerability Detail

An attacker could permanently disable the contract's functionality by creating a large number of empty entries in the `deposits` array. This can be achieved by calling `depositUSDC()` in combination with `withdrawUSDC()` a large number of times. This could be done in batches across several blocks. The exact same attack vector can be seen with the `withdraws` array which can be abused with the combination of `queueCrabForWithdrawal()` and `dequeueCrab()`.

The `depositUSDC()` function pushes a `Receipt` into the `deposits`:
```Solidity
    function depositUSDC(uint256 _amount) external {
        require(_amount >= minUSDCAmount, "deposit amount smaller than minimum OTC amount");

        IERC20(usdc).transferFrom(msg.sender, address(this), _amount);

        // update usd balance of user, add their receipt, and receipt index to user deposits index
        usdBalance[msg.sender] = usdBalance[msg.sender] + _amount;
        deposits.push(Receipt(msg.sender, _amount));
        userDepositsIndex[msg.sender].push(deposits.length - 1);

        emit USDCQueued(msg.sender, _amount, usdBalance[msg.sender], deposits.length - 1);
    }
```

The `withdrawUSDC()` function will delete a number of these `Receipt` objects from `deposits` based on the input `_amount`:
```Solidity
    function withdrawUSDC(uint256 _amount) external {
        require(!isAuctionLive, "auction is live");

        usdBalance[msg.sender] = usdBalance[msg.sender] - _amount;
        require(
            usdBalance[msg.sender] >= minUSDCAmount || usdBalance[msg.sender] == 0,
            "remaining amount smaller than minimum, consider removing full balance"
        );

        // start withdrawing from the users last deposit
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userDepositsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = deposits[userDepositsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete deposits[userDepositsIndex[msg.sender][i - 1]];
            }
        }
        IERC20(usdc).transfer(msg.sender, _amount);

        emit USDCDeQueued(msg.sender, _amount, usdBalance[msg.sender]);
    }
```

However the delete operation will only reset the `Receipt` object with default values. This can lead to a large "gap" of empty `Receipt` objects inside `deposits` array, which would cause the auction and netting functions to always revert with out of gas errors when looping over the array looking for a valid, non empty `Receipt`:
```Solidity
        while (remainingDeposits > 0) {
            uint256 queuedAmount = deposits[k].amount;
            Portion memory portion;
            if (queuedAmount == 0) {
                k++;
                continue;
            }
```

## Impact

An attacker can permanently render the contract useless.

## Code Snippet

`deposits` attack vector:
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L268
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L298
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L362-L367
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L577-L583

`withdraws` attack vector:
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L314
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L341
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L390-L396
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L691-L696

## Tool used

Manual Review

## Recommendation

Consider adding a function to "advance" the `depositsIndex` and the `withdrawsIndex`. As these indexes are updated only after all the processing is done, the contract cannot break out of this state and become functional again. Consider adding a function to correct this state and jump over the large "gap" of empty `Receipt` objects. This will disable the described attack vector. Here's a sample function for `depositsIndex`, the `withdrawsIndex` function could be done in the same manner:
```Solidity
    function advanceDepositsIndex(uint256 maxAdvance) external {
        uint256 i = depositsIndex;
        for (uint256 j = 0; j < maxAdvance; ++j) {
            i += j;
            if (deposits[i].amount == 0) {
                continue;
            } else {
                break;
            }
        }
        depositsIndex = i;
    }
```
