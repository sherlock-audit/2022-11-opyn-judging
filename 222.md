hyh

high

# External checkOrder can be used to invalidate any order, preventing the auction

## Summary

checkOrder() is external permissionless function that invokes _checkOrder() for a given order, that verifies it and **marks it as used**.

Griefing attack is possible this way: read the orders, front run the depositAuction() or withdrawAuction() with `checkOrder(order)`, making auction fail.

## Vulnerability Detail

Any auction can be prevented by front-running depositAuction() and withdrawAuction() calls with checkOrder() call for any order (say the first one) from the `WithdrawAuctionParams p` used.

## Impact

An attacker can block the auctions any time when it is beneficial to him.

As auction timing can have material impact on the users, the inability to perform it will lead to losses for them as the attacker will choose the moment to execute the attack to benefit himself at the expense of the participants.

Setting the severity to be high as this is controllable by the attacker unavailability of the core functionality and there are no low probability prerequisites.

## Code Snippet

checkOrder() is publicly available wrapper for _checkOrder():

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L447-L449

```solidity
    function checkOrder(Order memory _order) external {
        return _checkOrder(_order);
    }
```

`_checkOrder(_order)` uses the order nonce, so any subsequent runs after the first one will be reverted:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L451-L457

```solidity
    /**
     * @dev checks the expiry nonce and signer of an order
     * @param _order is the Order struct
     */
    function _checkOrder(Order memory _order) internal {
        _useNonce(_order.trader, _order.nonce);
        bytes32 structHash = keccak256(
```

It will be reverted by _useNonce(), that requires that the `_nonce` supplied isn't being used yet:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L751-L759

```solidity
    /**
     * @dev set nonce flag of the trader to true
     * @param _trader address of the signer
     * @param _nonce number that is to be traded only once
     */
    function _useNonce(address _trader, uint256 _nonce) internal {
        require(!nonces[_trader][_nonce], "Nonce already used");
        nonces[_trader][_nonce] = true;
    }
```

_checkOrder() is used by depositAuction():

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L487-L510

```solidity
    /**
     * @dev takes in orders from mm's to buy sqth and deposits the usd amount from the depositQueue into crab along with the eth from selling sqth
     * @param _p DepositAuction Params that contain orders, usdToDeposit, uniswap min amount and fee
     */
    function depositAuction(DepositAuctionParams calldata _p) external onlyOwner {
        _checkOTCPrice(_p.clearingPrice, false);
        /**
         * step 1: get eth from mm
         *     step 2: get eth from deposit usdc
         *     step 3: crab deposit
         *     step 4: flash deposit
         *     step 5: send sqth to mms
         *     step 6: send crab to depositors
         */
        uint256 initCrabBalance = IERC20(crab).balanceOf(address(this));
        uint256 initEthBalance = address(this).balance;

        uint256 sqthToSell = _debtToMint(_p.totalDeposit);
        // step 1 get all the eth in
        uint256 remainingToSell = sqthToSell;
        for (uint256 i = 0; i < _p.orders.length; i++) {
            require(_p.orders[i].isBuying, "auction order not buying sqth");
            require(_p.orders[i].price >= _p.clearingPrice, "buy order price less than clearing");
            _checkOrder(_p.orders[i]);
```

And withdrawAuction():

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L624-L645

```solidity
    /**
     * @dev takes in orders from mm's to sell sqth and withdraws the crab amount in q
     * @param _p Withdraw Params that contain orders, crabToWithdraw, uniswap min amount and fee
     */
    function withdrawAuction(WithdrawAuctionParams calldata _p) public onlyOwner {
        _checkOTCPrice(_p.clearingPrice, true);
        uint256 initWethBalance = IERC20(weth).balanceOf(address(this));
        uint256 initEthBalance = address(this).balance;
        /**
         * step 1: get sqth from mms
         *     step 2: withdraw from crab
         *     step 3: send eth to mms
         *     step 4: convert eth to usdc
         *     step 5: send usdc to withdrawers
         */

        // step 1 get sqth from mms
        uint256 sqthRequired = ICrabStrategyV2(crab).getWsqueethFromCrabAmount(_p.crabToWithdraw);
        uint256 toPull = sqthRequired;
        for (uint256 i = 0; i < _p.orders.length && toPull > 0; i++) {
            _checkOrder(_p.orders[i]);
            require(!_p.orders[i].isBuying, "auction order is not selling");
```

## Tool used

Manual Review

## Recommendation

As there looks to be no need for public checkOrder() consider making it `onlyOwner`.
