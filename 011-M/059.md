KingNFT

medium

# Miss to pop cleared 'userDepositsIndex' items

## Summary
The 'withdrawUSDC()' function is missing to  pop 'userDepositsIndex' items which have been cleared, causes unbound gas cost.

## Vulnerability Detail
```solidity
function withdrawUSDC(uint256 _amount) external {
    require(!isAuctionLive, "auction is live");

    usdBalance[msg.sender] = usdBalance[msg.sender] - _amount;
    require(
        usdBalance[msg.sender] >= minUSDCAmount || usdBalance[msg.sender] == 0,
        "remaining amount smaller than minimum, consider removing full balance"
    );

    uint256 toRemove = _amount;
    uint256 lastIndexP1 = userDepositsIndex[msg.sender].length;
    for (uint256 i = lastIndexP1; i > 0; i--) {
        Receipt storage r = deposits[userDepositsIndex[msg.sender][i - 1]];
        if (r.amount > toRemove) {
            r.amount -= toRemove;
            toRemove = 0;
            break;
        } else {
            toRemove -= r.amount;
            delete deposits[userDepositsIndex[msg.sender][i - 1]];
            // @audit miss userDepositsIndex[msg.sender].pop()
        }
    }
    IERC20(usdc).transfer(msg.sender, _amount);

    emit USDCDeQueued(msg.sender, _amount, usdBalance[msg.sender]);
}
```
## Impact
Unbound gas cost for 'withdrawUSDC()'
```solidity
Running 1 test for test/UnboundUSDCWithdrawGasCost.t.sol:UnboundUSDCWithdrawGasCost
[PASS] testUnboundUSDCWithdrawGasCost() (gas: 4116709419)
Logs:
  cleared but not popped userDepositsIndex items: 100 , gas cost for single withdrawUSDC: 210603
  cleared but not popped userDepositsIndex items: 200 , gas cost for single withdrawUSDC: 411200
  cleared but not popped userDepositsIndex items: 1000 , gas cost for single withdrawUSDC: 2016000
  cleared but not popped userDepositsIndex items: 2000 , gas cost for single withdrawUSDC: 4022000

Test result: ok. 1 passed; 0 failed; finished in 20.84s
```

Put the following test case into UnboundUSDCWithdrawGasCost.t.sol file of test directory.
And run
```solidity
forge test --match-test testUnboundUSDCWithdrawGasCost -vv
```
It will reproduce the above result.
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {BaseForkSetup} from "./BaseForkSetup.t.sol";
import {console} from "forge-std/console.sol";

contract UnboundUSDCWithdrawGasCost is BaseForkSetup {
    function setUp() public override {
        BaseForkSetup.setUp(); 

        vm.startPrank(0x57757E3D981446D585Af0D9Ae4d7DF6D64647806);
        usdc.transfer(depositor, 500000e6);
        vm.stopPrank();
    }

    function testUnboundUSDCWithdrawGasCost() public {
        netting.setMinUSDC(1e6);
        vm.startPrank(depositor);
        usdc.approve(address(netting), type(uint256).max);
        netting.depositUSDC(1000e6);

        uint256 i = 1;
        for (; i <= 100; ++i) {
            netting.depositUSDC(1e6);
            netting.withdrawUSDC(1e6);
        }
        uint256 gasBefore = gasleft();
        netting.withdrawUSDC(1e6);
        uint256 gasAfter = gasleft();
        console.log("cleared but not popped userDepositsIndex items:", i - 1, ", gas cost for single withdrawUSDC:", gasBefore - gasAfter);

        for (; i <= 200; ++i) {
            netting.depositUSDC(1e6);
            netting.withdrawUSDC(1e6);
        }
        gasBefore = gasleft();
        netting.withdrawUSDC(1e6);
        gasAfter = gasleft();
        console.log("cleared but not popped userDepositsIndex items:", i - 1, ", gas cost for single withdrawUSDC:", gasBefore - gasAfter);


        for (; i <= 1000; ++i) {
            netting.depositUSDC(1e6);
            netting.withdrawUSDC(1e6);
        }
        gasBefore = gasleft();
        netting.withdrawUSDC(1e6);
        gasAfter = gasleft();
        console.log("cleared but not popped userDepositsIndex items:", i - 1, ", gas cost for single withdrawUSDC:", gasBefore - gasAfter);

        for (; i <= 2000; ++i) {
            netting.depositUSDC(1e6);
            netting.withdrawUSDC(1e6);
        }
        gasBefore = gasleft();
        netting.withdrawUSDC(1e6);
        gasAfter = gasleft();
        console.log("cleared but not popped userDepositsIndex items:", i - 1, ", gas cost for single withdrawUSDC:", gasBefore - gasAfter);
    }
}
```

## Code Snippet
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L278-L304
## Tool used

Manual Review

## Recommendation
Pop cleared userDepositsIndex items.
