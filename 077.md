0x52

medium

# CrabNetting can be DOS'd by partially dequeuing withdraws/deposits

## Summary

Minimum deposit/withdraw values are used to prevent users from DOS'ing auctions with large number of tiny deposits. This protection is ineffective when there is a no live auction because a malicious user can repeatedly queue and dequeue to leave a very large number of 1 wei deposits.

## Vulnerability Detail

    function depositUSDC(uint256 _amount) external {
        require(_amount >= minUSDCAmount, "deposit amount smaller than minimum OTC amount");

        IERC20(usdc).transferFrom(msg.sender, address(this), _amount);

        // update usd balance of user, add their receipt, and receipt index to user deposits index
        usdBalance[msg.sender] = usdBalance[msg.sender] + _amount;
        deposits.push(Receipt(msg.sender, _amount));
        userDepositsIndex[msg.sender].push(deposits.length - 1);

        emit USDCQueued(msg.sender, _amount, usdBalance[msg.sender], deposits.length - 1);
    }

When queuing USDC for an auction, the user must deposit a minimum of `minUSDCAmount`. After each deposit a new deposit is pushed to `deposits`.

    function withdrawUSDC(uint256 _amount) external {
        require(!isAuctionLive, "auction is live");

        usdBalance[msg.sender] = usdBalance[msg.sender] - _amount;

        require(
            usdBalance[msg.sender] >= minUSDCAmount || usdBalance[msg.sender] == 0,
            "remaining amount smaller than minimum, consider removing full balance"
        );

        // start withdrawing from the users last deposit
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userDepositsIndex[msg.sender].length;

        //fully fill their withdraw
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = deposits[userDepositsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete deposits[userDepositsIndex[msg.sender][i - 1]];
            }
        }
        IERC20(usdc).transfer(msg.sender, _amount);
        emit USDCDeQueued(msg.sender, _amount, usdBalance[msg.sender]);
    }

When dequeuing USDC for an auction, the minimum USDC deposit is applied differently. The only thing it enforces is that the sum of all user deposits is still greater than `minUSDCAmount`. This is where the DOS protection fails. A user can queue 2 `minUSDCAmount` deposits then dequeue `minUSDCAmount - 1`. Leaving 1 `minUSDCAmount` deposit and 1 `1 wei` deposit.

Example:
Assume `minUSDCAmount = 100_000` and `isAuctionLive = false`. A user call `depositUSDC(100_000)` twice, pushing 2 deposits.  Next they call `withdrawUSDC(99_999)`. Their `usdBalance` is 200_000 before the call and is 100_001 after it is decreased by the withdraw amount. Since this is above `minUSDCAmount`, the withdraw goes through. Deposits are dequeued in a 'last in first out' sequence. The result is that after the withdraw the user now has 1 deposit of 100_000 and 1 deposit of 1. The user can repeat `depositUSDC(100_000)` then `withdrawUSDC(99_999)`. Because of how deposits are dequeued each iteration will leave 1 additional deposit of 1. This can be repeated to add a very large number of `1 wei` deposits

## Impact

Auction is DOS'd

## Code Snippet

https://github.com/opynfinance/squeeth-monorepo/blob/d217c66ca17c2516ed288ae87d4757b71ea67f7d/packages/crab-netting/src/CrabNetting.sol#L278-L304

## Tool used

Manual Review

## Recommendation

Require that users can only queue deposits when `isAuctionLive = true`