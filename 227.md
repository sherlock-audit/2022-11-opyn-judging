hyh

medium

# withdrawAuction distribution can be altered

## Summary

If withdrawAuction() be called when an auction is not live, a griefing attack is possible: Bob the withdrawer can front-run the withdrawAuction() with dequeueCrab(), removing his withdrawal, causing the withdrawAuction() accounting malfunction is a sense that it will distribute not 100% of the rewards, but less as the function parameters were determined as if Bob's withdrawal was there.

This is possible as all other parameters determining the sum are dynamic, while `_p.crabToWithdraw` is pre-determined by function caller (auction operator).

The remaining `100% - actual` part will be frozen on the contract balance as the corresponding user entries be successfully deleted. 

## Vulnerability Detail

withdrawAuction() determines the WETH `amountIn` to swap for USDC to distribute dynamically, and sum of `withdraw.amount` is dynamic given that auction is not live. I.e. both USDC total sum to distribute and sum of `withdraw.amount` will be reduced by Bob's actions, but `_p.crabToWithdraw` will stay the same.

I.e. the `usdcAmount = (((withdraw.amount * 1e18) / _p.crabToWithdraw) * usdcReceived) / 1e18` formula has `_p.crabToWithdraw` determined by the auction organizer beforehand, while `usdcReceived` is dynamic and cumulative sum of `withdraw.amount` can be manipulated by Bob, who removed his crabs from the queue.

This means that `usdcReceived` and the sum of `withdraw.amount` was reduced by Bob's dequeueCrab(), but `_p.crabToWithdraw` stayed the same, being determined by the state where Bob's queue item was present. I.e. sum of `withdraw.amount` no longer corresponds to `_p.crabToWithdraw` and withdrawers will receive less than 100% of `usdcReceived` in total.

This will cause a shortage of actual withdrawal queue, due to which the remaining withdrawers will receive less funds.

As there are no rescue function, the remaining USDC will be frozen on contract balance.

## Impact

Withdrawers will lose funds, receiving less USDC than was due. The unallocated funds will be permanently frozen on the contract balance.

Setting the severity to be medium as `isAuctionLive == false` is required for dequeueCrab().

## Code Snippet

withdrawAuction() determines how much WETH to sell dynamically and base USDC amount calculations on `_p.crabToWithdraw`:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L624-L722

```solidity
    /**
     * @dev takes in orders from mm's to sell sqth and withdraws the crab amount in q
     * @param _p Withdraw Params that contain orders, crabToWithdraw, uniswap min amount and fee
     */
    function withdrawAuction(WithdrawAuctionParams calldata _p) public onlyOwner {
        _checkOTCPrice(_p.clearingPrice, true);
        uint256 initWethBalance = IERC20(weth).balanceOf(address(this));
        uint256 initEthBalance = address(this).balance;
        /**
         *     step 1: get sqth from mms
         *     step 2: withdraw from crab
         *     step 3: send eth to mms
         *     step 4: convert eth to usdc
         *     step 5: send usdc to withdrawers
         */

        // step 1 get sqth from mms
        uint256 sqthRequired = ICrabStrategyV2(crab).getWsqueethFromCrabAmount(_p.crabToWithdraw);
        uint256 toPull = sqthRequired;
        for (uint256 i = 0; i < _p.orders.length && toPull > 0; i++) {
            ...
        }

        // step 2 withdraw from crab
        ICrabStrategyV2(crab).withdraw(_p.crabToWithdraw);

        // step 3 pay all mms
        IWETH(weth).deposit{value: address(this).balance - initEthBalance}();
        toPull = sqthRequired;
        uint256 sqthQuantity;
        for (uint256 i = 0; i < _p.orders.length && toPull > 0; i++) {
            ...
        }

        // step 4 convert to USDC
        ISwapRouter.ExactInputSingleParams memory params = ISwapRouter.ExactInputSingleParams({
            tokenIn: address(weth),
            tokenOut: address(usdc),
            fee: _p.ethUSDFee,
            recipient: address(this),
            deadline: block.timestamp,
            amountIn: (IERC20(weth).balanceOf(address(this)) - initWethBalance),
            amountOutMinimum: _p.minUSDC,
            sqrtPriceLimitX96: 0
        });
        uint256 usdcReceived = swapRouter.exactInputSingle(params);

        // step 5 pay all withdrawers and mark their withdraws as done
        uint256 remainingWithdraws = _p.crabToWithdraw;
        uint256 j = withdrawsIndex;
        uint256 usdcAmount;
        while (remainingWithdraws > 0) {
            Receipt memory withdraw = withdraws[j];
            if (withdraw.amount == 0) {
                j++;
                continue;
            }
            if (withdraw.amount <= remainingWithdraws) {
                // full usage
                remainingWithdraws -= withdraw.amount;
                crabBalance[withdraw.sender] -= withdraw.amount;

                // send proportional usdc
                usdcAmount = (((withdraw.amount * 1e18) / _p.crabToWithdraw) * usdcReceived) / 1e18;
                IERC20(usdc).transfer(withdraw.sender, usdcAmount);
                emit CrabWithdrawn(withdraw.sender, withdraw.amount, usdcAmount, j);
                delete withdraws[j];
                j++;
            } else {
                withdraws[j].amount -= remainingWithdraws;
                crabBalance[withdraw.sender] -= remainingWithdraws;

                // send proportional usdc
                usdcAmount = (((remainingWithdraws * 1e18) / _p.crabToWithdraw) * usdcReceived) / 1e18;
                IERC20(usdc).transfer(withdraw.sender, usdcAmount);
                emit CrabWithdrawn(withdraw.sender, remainingWithdraws, usdcAmount, j);

                remainingWithdraws = 0;
            }
        }
        withdrawsIndex = j;
        isAuctionLive = false;
    }
```

## Tool used

Manual Review

## Recommendation

Consider ensuring that the auction is run with the active flag only:

```solidity
    /**
     * @dev takes in orders from mm's to sell sqth and withdraws the crab amount in q
     * @param _p Withdraw Params that contain orders, crabToWithdraw, uniswap min amount and fee
     */
    function withdrawAuction(WithdrawAuctionParams calldata _p) public onlyOwner {
    +   require(isAuctionLive, "Auction needs to be started");  
        _checkOTCPrice(_p.clearingPrice, true);
        uint256 initWethBalance = IERC20(weth).balanceOf(address(this));
        uint256 initEthBalance = address(this).balance;
```

