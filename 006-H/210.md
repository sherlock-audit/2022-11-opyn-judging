chainNue

high

# `checkOrder` external function will block Auction process

## Summary

There is a `checkOrder` function which is external and user can call it (even when the `isAuctionLive` is true). malicious user can just keep calling the function to cancel the order (set the nonce to true) thus the `depositAuction()` and `withdrawAuction()` will revert if their order is part of the orders in the Auction.

## Vulnerability Detail

inside `depositAuction()` and `withdrawAuction()` there is a check `_checkOrder` to verify the order. the `_checkOrder` internal function will also check if the `nonce` is already being used or not. This means, calling `_checkOrder` for the same order will invalidate the order. Therefore if the order is being packed to be processed in the Auction, this single order will revert the entire process (because of the nonce is already being used).

There is a `checkOrder` external function which can be called by user, which is not restricted when the Auction is happening. So, if the user call this `checkOrder` function with the correct `Order`, it will set the `nonce` to be true, and their order which is in Auction will make the whole Auction process to be failed / reverted.

## Impact

Auction will be blocked

## Code Snippet

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L447-L449

```solidity
File: CrabNetting.sol
447:     function checkOrder(Order memory _order) external {
448:         return _checkOrder(_order);
449:     }
450: 
451:     /**
452:      * @dev checks the expiry nonce and signer of an order
453:      * @param _order is the Order struct
454:      */
455:     function _checkOrder(Order memory _order) internal {
456:         _useNonce(_order.trader, _order.nonce);
457:         bytes32 structHash = keccak256(
458:             abi.encode(
459:                 _CRAB_NETTING_TYPEHASH,
460:                 _order.bidId,
461:                 _order.trader,
462:                 _order.quantity,
463:                 _order.price,
464:                 _order.isBuying,
465:                 _order.expiry,
466:                 _order.nonce
467:             )
468:         );
469: 
470:         bytes32 hash = _hashTypedDataV4(structHash);
471:         address offerSigner = ECDSA.recover(hash, _order.v, _order.r, _order.s);
472:         require(offerSigner == _order.trader, "Signature not correct");
473:         require(_order.expiry >= block.timestamp, "order expired");
474:     }

File: CrabNetting.sol
756:     function _useNonce(address _trader, uint256 _nonce) internal {
757:         require(!nonces[_trader][_nonce], "Nonce already used");
758:         nonces[_trader][_nonce] = true;
759:     }
```

## Tool used

Manual Review

## Recommendation

Depends on the design of protocol, if the `checkOrder` is mandatory for user to cancel their order, include the check condition 
```solidity
require(!isAuctionLive, "auction is live");
```
so user can not cancel their order when the Auction in progress.
or just remove the `checkOrder` external function if that's not important.