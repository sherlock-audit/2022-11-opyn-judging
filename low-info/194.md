hansfriese

medium

# Wrong events for critical parameter changes

## Summary

Function `setOTCPriceTolerance` is used to set the critical parameter `otcPriceTolerance` but a wrong event is emitted when there is a change. This can confuse users and lead to unintended results affecting the protocol's reputation.

## Vulnerability Detail

`otcPriceTolerance` is a critical parameter that prevents an operator to do netting with a wrong price and I believe the users are sensitive to this because they deposit/queue requests without specifying kind of minimum out amount.
But in the admin setter function(CrabNetting.sol#L744), a wrong event is emitted and this will confuse users and can lead to unintended results.

```solidity
function setOTCPriceTolerance(uint256 _otcPriceTolerance) external onlyOwner {
    // Tolerance cannot be more than 20%
    require(_otcPriceTolerance <= MAX_OTC_PRICE_TOLERANCE, "Price tolerance has to be less than 20%");
    uint256 previousOtcTolerance = auctionTwapPeriod; //@audit should be previousOtcTolerance = otcPriceTolerance

    otcPriceTolerance = _otcPriceTolerance;

    emit SetOTCPriceTolerance(previousOtcTolerance, _otcPriceTolerance);
}
```

Other than this, there is another place (CrabNetting.sol#L413) where a wrong event is emitted (wrong withdraw amount) that will affect the off-chain tracking of user withdraw queue.

```solidity
function netAtPrice(uint256 _price, uint256 _quantity) external onlyOwner {
    ...
    // process withdraws and send usdc
    i = withdrawsIndex;
    while (crabQuantity > 0) {
        Receipt memory withdraw = withdraws[i];
        if (withdraw.amount == 0) {
            i++;
            continue;
        }
        if (withdraw.amount <= crabQuantity) {
            crabQuantity = crabQuantity - withdraw.amount;
            crabBalance[withdraw.sender] -= withdraw.amount;
            amountToSend = (withdraw.amount * _price) / 1e18;
            IERC20(usdc).transfer(withdraw.sender, amountToSend);

            emit CrabWithdrawn(withdraw.sender, withdraw.amount, amountToSend, i);

            delete withdraws[i];
            i++;
        } else {
            withdraws[i].amount = withdraw.amount - crabQuantity;
            crabBalance[withdraw.sender] -= crabQuantity;
            amountToSend = (crabQuantity * _price) / 1e18;
            IERC20(usdc).transfer(withdraw.sender, amountToSend);

            emit CrabWithdrawn(withdraw.sender, withdraw.amount, amountToSend, i);//@audit should be withdraw.sender, crabQuantity, amountToSend, i

            crabQuantity = 0;
        }
    }
    ...
}
```

## Impact

Critical changes of the protocol is incorrectly tracked.

## Code Snippet

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L744
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L390-L419

## Tool used

Manual Review

## Recommendation

Use the correct values for event emissions as suggested above.