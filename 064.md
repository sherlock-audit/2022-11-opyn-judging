KingNFT

medium

# Miss to pop cleared 'userWithdrawsIndex' items

## Summary
The 'dequeueCrab()' function is missing to pop 'userWithdrawsIndex' items which have been cleared, causes unbound gas cost.

## Vulnerability Detail
```solidity
function dequeueCrab(uint256 _amount) external {
    require(!isAuctionLive, "auction is live");
    crabBalance[msg.sender] = crabBalance[msg.sender] - _amount;
    require(
        crabBalance[msg.sender] >= minCrabAmount || crabBalance[msg.sender] == 0,
        "remaining amount smaller than minimum, consider removing full balance"
    );
    uint256 toRemove = _amount;
    uint256 lastIndexP1 = userWithdrawsIndex[msg.sender].length;
    for (uint256 i = lastIndexP1; i > 0; i--) {
        Receipt storage r = withdraws[userWithdrawsIndex[msg.sender][i - 1]];
        if (r.amount > toRemove) {
            r.amount -= toRemove;
            toRemove = 0;
            break;
        } else {
            toRemove -= r.amount;
            delete withdraws[userWithdrawsIndex[msg.sender][i - 1]];
            // @audit miss userWithdrawsIndex[msg.sender].pop()
        }
    }
    IERC20(crab).transfer(msg.sender, _amount);
    emit CrabDeQueued(msg.sender, _amount, crabBalance[msg.sender]);
}
```
## Impact
Unbound gas cost for 'dequeueCrab()'.
```solidity
Running 1 test for test/UnboundCrabWithdrawGasCost.t.sol:UnboundCrabWithdrawGasCost
[PASS] testUnboundCrabWithdrawGasCost() (gas: 4114352417)
Logs:
  cleared but not popped userWithdrawsIndex items: 100 , gas cost for single dequeueCrab: 209253
  cleared but not popped userWithdrawsIndex items: 200 , gas cost for single dequeueCrab: 409850
  cleared but not popped userWithdrawsIndex items: 1000 , gas cost for single dequeueCrab: 2014650
  cleared but not popped userWithdrawsIndex items: 2000 , gas cost for single dequeueCrab: 4020650

Test result: ok. 1 passed; 0 failed; finished in 19.99s
```

Put the following test case into UnboundCrabWithdrawGasCost.t.sol file of test directory.
And run
```solidity
forge test --match-test testUnboundCrabWithdrawGasCost -vv
```
It will reproduce the above result.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {BaseForkSetup} from "./BaseForkSetup.t.sol";
import {console} from "forge-std/console.sol";

contract UnboundCrabWithdrawGasCost is BaseForkSetup {
    function setUp() public override {
        BaseForkSetup.setUp(); 

        vm.prank(0x06CECFbac34101aE41C88EbC2450f8602b3d164b);
        crab.transfer(withdrawer, 20e18);
        vm.stopPrank();
    }

    function testUnboundCrabWithdrawGasCost() public {
        vm.startPrank(withdrawer);
        crab.approve(address(netting), type(uint256).max);
        netting.queueCrabForWithdrawal(1000e9);

        uint256 i = 1;
        for (; i <= 100; ++i) {
            netting.queueCrabForWithdrawal(1e9);
            netting.dequeueCrab(1e9);
        }
        uint256 gasBefore = gasleft();
        netting.dequeueCrab(1e9);
        uint256 gasAfter = gasleft();
        console.log("cleared but not popped userWithdrawsIndex items:", i - 1, ", gas cost for single dequeueCrab:", gasBefore - gasAfter);

        for (; i <= 200; ++i) {
            netting.queueCrabForWithdrawal(1e6);
            netting.dequeueCrab(1e6);
        }
        gasBefore = gasleft();
        netting.dequeueCrab(1e9);
        gasAfter = gasleft();
        console.log("cleared but not popped userWithdrawsIndex items:", i - 1, ", gas cost for single dequeueCrab:", gasBefore - gasAfter);


        for (; i <= 1000; ++i) {
            netting.queueCrabForWithdrawal(1e6);
            netting.dequeueCrab(1e6);
        }
        gasBefore = gasleft();
        netting.dequeueCrab(1e9);
        gasAfter = gasleft();
        console.log("cleared but not popped userWithdrawsIndex items:", i - 1, ", gas cost for single dequeueCrab:", gasBefore - gasAfter);

        for (; i <= 2000; ++i) {
            netting.queueCrabForWithdrawal(1e6);
            netting.dequeueCrab(1e6);
        }
        gasBefore = gasleft();
        netting.dequeueCrab(1e9);
        gasAfter = gasleft();
        console.log("cleared but not popped userWithdrawsIndex items:", i - 1, ", gas cost for single dequeueCrab:", gasBefore - gasAfter);
    }
}
```
## Code Snippet
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L323-L346
## Tool used

Manual Review

## Recommendation
Pop cleared userWithdrawsIndex items.
