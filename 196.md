hansfriese

medium

# Traders might get unwanted results due to partially filled of orders

## Summary

During the auctions, the traders sign orders (buying/selling SQTH) with quantity specified but it is possible for auctions to partially fill these orders. This might be not what the traders wanted when they signed the order.

## Vulnerability Detail

Across the protocol, `Order` structure is used to represent the position of the traders for both selling and buying SQTH.
These orders are signed by the trader with an unused nonce.

```solidity
/// @dev order struct for a signed order from market maker
struct Order {
    uint256 bidId;
    address trader;
    uint256 quantity;
    uint256 price;
    bool isBuying;
    uint256 expiry;
    uint256 nonce;
    uint8 v;
    bytes32 r;
    bytes32 s;
}
```

Looking at the actual auction functions, it is possible that these orders are partially filled and that nonce is set to used.

```solidity
// #L491
function depositAuction(DepositAuctionParams calldata _p) external onlyOwner {
    ...
    // step 1 get all the eth in
    uint256 remainingToSell = sqthToSell;
    for (uint256 i = 0; i < _p.orders.length; i++) {
        require(_p.orders[i].isBuying, "auction order not buying sqth");
        require(_p.orders[i].price >= _p.clearingPrice, "buy order price less than clearing");
        _checkOrder(_p.orders[i]);
        if (_p.orders[i].quantity >= remainingToSell) {
            IWETH(weth).transferFrom(
                _p.orders[i].trader, address(this), (remainingToSell * _p.clearingPrice) / 1e18
            );
            remainingToSell = 0;
            break;
        } else {
            IWETH(weth).transferFrom(
                _p.orders[i].trader, address(this), (_p.orders[i].quantity * _p.clearingPrice) / 1e18
            );
            remainingToSell -= _p.orders[i].quantity;
        }
    }
    require(remainingToSell == 0, "not enough buy orders for sqth");
    ...
}
// #L628
function withdrawAuction(WithdrawAuctionParams calldata _p) public onlyOwner {
    ...
    // step 1 get sqth from mms
    uint256 sqthRequired = ICrabStrategyV2(crab).getWsqueethFromCrabAmount(_p.crabToWithdraw);
    uint256 toPull = sqthRequired;
    for (uint256 i = 0; i < _p.orders.length && toPull > 0; i++) {
        _checkOrder(_p.orders[i]);
        require(!_p.orders[i].isBuying, "auction order is not selling");
        require(_p.orders[i].price <= _p.clearingPrice, "sell order price greater than clearing");
        if (_p.orders[i].quantity < toPull) {
            toPull -= _p.orders[i].quantity;
            IERC20(sqth).transferFrom(_p.orders[i].trader, address(this), _p.orders[i].quantity);
        } else {
            IERC20(sqth).transferFrom(_p.orders[i].trader, address(this), toPull);
            toPull = 0;
        }
    }
    ...
}
```

So it is possible that an order of quantity 1000 SQTH (or ETH) are filled with tiny amount and this might be not what the traders wanted.
The `Order` structure does not explain in what extent the `quantity` is to be filled and it is likely the traders understand this as exact amount.
Because the order `price` can change according to the `quantity` from the trader's perspective and he might have signed an order with some expectations. (e.g. a trader might have his own formula between the quantity and the price like he is willing to purchase SQTH at a price of 0.5ETH/SQTH as long as he could get at least 100SQTH).

Although the partial fill does not harm traders, a more specific approach is recommended.

## Impact

Traders might get unwanted results due to partially filled of orders.

## Code Snippet

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L18-L29
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L491-L524
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L628-L654

## Tool used

Manual Review

## Recommendation

Make sure to fill orders full to `quantity` or add a new variable in the `Order` structure to allow traders specify their minimum expectation.