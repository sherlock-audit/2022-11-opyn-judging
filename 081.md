Haruxe

medium

# Malicious Nonce DOS in `CrabNetting.sol`

## Summary
The accessibility of setting certain nonces to be used up for a certain user/order with the `setNonceTrue()` function can cause `depositAuction()` and `withdrawAuction()` to revert upon seeing the used nonce if they are in the `DepositAuctionParams`.

### CrabNetting.sol#L230-L237
```solidity
    /**
     * @notice set nonce to true
     * @param _nonce the number to be set true
     */
    function setNonceTrue(uint256 _nonce) external {
        nonces[msg.sender][_nonce] = true;
        emit NonceTrue(msg.sender, _nonce);
    }
```

## Vulnerability Detail
Malicious MMs who have orders ready to be deposited/withdrawn by the multisig/owner may call the `setNonceTrue()` function right before the `depositAuction()` or `withdrawAuction()` is called. This will cause the function to revert when calling `_checkOrder()`, as the nonce is already used.

### CrabNetting.sol#L491-523
```solidity
    function depositAuction(DepositAuctionParams calldata _p) external onlyOwner {
        _checkOTCPrice(_p.clearingPrice, false);
        ...
        uint256 initCrabBalance = IERC20(crab).balanceOf(address(this));
        uint256 initEthBalance = address(this).balance;

        uint256 sqthToSell = _debtToMint(_p.totalDeposit);
        // step 1 get all the eth in
        uint256 remainingToSell = sqthToSell;
        for (uint256 i = 0; i < _p.orders.length; i++) {
            require(_p.orders[i].isBuying, "auction order not buying sqth");
            require(_p.orders[i].price >= _p.clearingPrice, "buy order price less than clearing");
            _checkOrder(_p.orders[i]);
            if (_p.orders[i].quantity >= remainingToSell) {
                IWETH(weth).transferFrom(
                    _p.orders[i].trader, address(this), (remainingToSell * _p.clearingPrice) / 1e18
                );
                remainingToSell = 0;
                break;
            } else {
                IWETH(weth).transferFrom(
                    _p.orders[i].trader, address(this), (_p.orders[i].quantity * _p.clearingPrice) / 1e18
                );
                remainingToSell -= _p.orders[i].quantity;
            }
        }
        ...
    }
```

### CrabNetting.sol#L455-474
```solidity
    function _checkOrder(Order memory _order) internal {
        _useNonce(_order.trader, _order.nonce);
        bytes32 structHash = keccak256(
            abi.encode(
                _CRAB_NETTING_TYPEHASH,
                _order.bidId,
                _order.trader,
                _order.quantity,
                _order.price,
                _order.isBuying,
                _order.expiry,
                _order.nonce
            )
        );

        bytes32 hash = _hashTypedDataV4(structHash);
        address offerSigner = ECDSA.recover(hash, _order.v, _order.r, _order.s);
        require(offerSigner == _order.trader, "Signature not correct");
        require(_order.expiry >= block.timestamp, "order expired");
    }
```
## Impact
`depositAuction()` and `withdrawAuction()` will no longer work with the used order inside of the parameters. This will cause enormous trouble and lead to extensive debugging for point of failure.
## Code Snippet
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L230-L237
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L455-L474
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L491-L551
## Tool used

Manual Review

## Recommendation
Limit nonce manipulation to internal only.