HonorLt

medium

# FILO can cause DOS

## Summary
First in, first out, can become insolvent if ```userDepositsIndex``` or ```userWithdrawsIndex``` grow too large, the iterations will revert.

## Vulnerability Detail
```userDepositsIndex``` or ```userWithdrawsIndex``` track indexes of user deposits/withdraws. When the user calls ```withdrawUSDC``` or ```dequeueCrab``` the system uses these indexes to determine the iteration start index. As an example, I take ```withdrawUSDC``` but the same applies to ```dequeueCrab```:
```solidity
        // start withdrawing from the users last deposit
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userDepositsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = deposits[userDepositsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete deposits[userDepositsIndex[msg.sender][i - 1]];
            }
        }
```

The problem is that after the whole amount is used, the deposit is deleted (```delete deposits[userDepositsIndex[msg.sender][i - 1]];```) but ```deposits``` is an array, so this does not delete the actual element of the array but nullifies the value. Deleted deposit becomes:
```solidity
 address: sender 0x0000000000000000000000000000000000000000
 uint256: amount 0
```
But an empty deposit still belongs to the array and because the ```userDepositsIndex``` is not popped, the next time it will iterate over the same empty deposits again and delete it again and again until it finds a non-empty deposit that is sufficient to cover the requested amount.

## Impact
User funds can become stuck if the ```deposits``` (or ```withdraws```) grow quite large, then the user tries to partially withdraw funds, but the oldest funds become inaccessible because the iterations exceed the block gas limit.

Example:
1) User deposits 50 times 100 tokens each. The user's total balance is 5000.
2) User withdraws 4900 tokens. All the deposits except the first one are deleted. The user's balance is now 100.
3) User makes new deposits on top of old deposits.
4) User tries to withdraw everything. The first deposit can no longer be processed because the deleted deposits array is too large to iterate until the end.

## Code Snippet

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L287-L300

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L330-L343

Simplified PoC to experiment:
```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.13;

/// @dev receipt used to store deposits and withdraws
struct Receipt {
    /// @dev address of the depositor or withdrawer
    address sender;
    /// @dev usdc amount to queue for deposit or crab amount to queue for withdrawal
    uint256 amount;
}

contract CrabNetting {

    /// @dev usd amount to deposit for an address
    mapping(address => uint256) public usdBalance;

    /// @dev array of deposit receipts
    Receipt[] public deposits;

    /// @dev indexes of deposit receipts of an address
    mapping(address => uint256[]) public userDepositsIndex;

    function depositUSDC(uint256 _amount) external {
        usdBalance[msg.sender] = usdBalance[msg.sender] + _amount;

        deposits.push(Receipt(msg.sender, _amount));
        userDepositsIndex[msg.sender].push(deposits.length - 1);
    }

    function withdrawUSDC(uint256 _amount) external {
        usdBalance[msg.sender] = usdBalance[msg.sender] - _amount;

        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userDepositsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = deposits[userDepositsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete deposits[userDepositsIndex[msg.sender][i - 1]];
            }
        }
    }

    function lastIndexP1View() external view returns (uint256 lastIndexP1) {
        lastIndexP1 = userDepositsIndex[msg.sender].length;
    }
}


contract Test {

    function prepare(address _crabNetting, uint256 _iterations) external {
        CrabNetting crabNetting = CrabNetting(_crabNetting);

        for (uint256 i = 1; i <= _iterations; i++) {
            crabNetting.depositUSDC(i * 100);
        }
    }

    function withdraw(address _crabNetting, uint256 _amount) external {
        CrabNetting crabNetting = CrabNetting(_crabNetting);

        crabNetting.withdrawUSDC(_amount);
    }
}
```

## Tool used

Manual Review

## Recommendation
Consider popping out used ```userDepositsIndex``` or ```userWithdrawsIndex``` indexes when the corresponding ```deposits``` or ```withdraws``` are deleted. Or process it in first in first out principle and track the last processed indexes.