dipp

medium

# ```isAuctionLive``` could be set to true indefinitely, leading to users being unable to withdraw their assets

## Summary

If the admin is unable to change ```isAuctionLive``` to false, users could have stuck funds in the ```CrabNetting.sol``` contract.

## Vulnerability Detail

The ```isAuctionLive``` state variable is only set to false if the ```toggleAuctionLive```, ```depositAuction``` or ```withdrawAuction``` functions in ```CrabNetting.sol``` are called by the admin. If for some reason, the admin is unable to call these functions, users would be unable to call the ```withdrawUSDC``` and ```dequeueCrab``` function and could have their deposited crab and USDC tokens stuck in the contract.

## Impact

Users are unable to withdraw their funds.

## Code Snippet

[CrabNetting.sol:withdrawUSDC#L278-L304](https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L278-L304):
```solidity
    function withdrawUSDC(uint256 _amount) external {
        require(!isAuctionLive, "auction is live");

        usdBalance[msg.sender] = usdBalance[msg.sender] - _amount;
        require(
            usdBalance[msg.sender] >= minUSDCAmount || usdBalance[msg.sender] == 0,
            "remaining amount smaller than minimum, consider removing full balance"
        );

        // start withdrawing from the users last deposit
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userDepositsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = deposits[userDepositsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete deposits[userDepositsIndex[msg.sender][i - 1]];
            }
        }
        IERC20(usdc).transfer(msg.sender, _amount);

        emit USDCDeQueued(msg.sender, _amount, usdBalance[msg.sender]);
    }
```

[CrabNetting.sol:dequeueCrab#L323-L346](https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L323-L346):
```solidity
    function dequeueCrab(uint256 _amount) external {
        require(!isAuctionLive, "auction is live");
        crabBalance[msg.sender] = crabBalance[msg.sender] - _amount;
        require(
            crabBalance[msg.sender] >= minCrabAmount || crabBalance[msg.sender] == 0,
            "remaining amount smaller than minimum, consider removing full balance"
        );
        // deQueue crab from the last, last in first out
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userWithdrawsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = withdraws[userWithdrawsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete withdraws[userWithdrawsIndex[msg.sender][i - 1]];
            }
        }
        IERC20(crab).transfer(msg.sender, _amount);
        emit CrabDeQueued(msg.sender, _amount, crabBalance[msg.sender]);
    }
```

## Tool used

Manual Review

## Recommendation

Consider adding functionality that would allow anyone to set ```isAuctionLive``` to false once a certain amount of time has passed since it was set to true.