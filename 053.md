Haruxe

high

# A Couple Unbound Arrays Will Eventually Brick Functions

## Summary
Due to the architecture of `CrabNetting.sol`, the `withdraws` and `deposits` arrays will grow exceedingly in length.

CrabNetting.sol#L136-L139
```solidity
    /// @dev array of deposit receipts
    Receipt[] public deposits;
    /// @dev array of withdrawal receipts
    Receipt[] public withdraws;
```
This is because during every `USDC` withdraw and deposit, the new item is simply pushed onto the array - as well as the withdraw function not properly deleting a deposit from the array with the `delete` keyword; which keeps the length the same.

### The `deposits` array being `push`ed and `delete`d
CrabNetting.sol#L261-L272
```solidity
function depositUSDC(uint256 _amount) external {
        require(_amount >= minUSDCAmount, "deposit amount smaller than minimum OTC amount");

        IERC20(usdc).transferFrom(msg.sender, address(this), _amount);

        // update usd balance of user, add their receipt, and receipt index to user deposits index
        usdBalance[msg.sender] = usdBalance[msg.sender] + _amount;
        deposits.push(Receipt(msg.sender, _amount));
        userDepositsIndex[msg.sender].push(deposits.length - 1);

        emit USDCQueued(msg.sender, _amount, usdBalance[msg.sender], deposits.length - 1);
    }
```

CrabNetting.sol#L278-L304
```solidity
    function withdrawUSDC(uint256 _amount) external {
        require(!isAuctionLive, "auction is live");


        usdBalance[msg.sender] = usdBalance[msg.sender] - _amount;
        require(
            usdBalance[msg.sender] >= minUSDCAmount || usdBalance[msg.sender] == 0,
            "remaining amount smaller than minimum, consider removing full balance"
        );


        // start withdrawing from the users last deposit
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userDepositsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = deposits[userDepositsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete deposits[userDepositsIndex[msg.sender][i - 1]];
            }
        }
        IERC20(usdc).transfer(msg.sender, _amount);


        emit USDCDeQueued(msg.sender, _amount, usdBalance[msg.sender]);
    }
```

### The `withdraws` array being `push`ed and `delete`d
CrabNetting.sol#L306-L317
```solidity
    /**
     * @notice queue Crab for withdraw from crab strategy
     * @param _amount crab amount to withdraw
     */
    function queueCrabForWithdrawal(uint256 _amount) external {
        require(_amount >= minCrabAmount, "withdraw amount smaller than minimum OTC amount");
        IERC20(crab).transferFrom(msg.sender, address(this), _amount);
        crabBalance[msg.sender] = crabBalance[msg.sender] + _amount;
        withdraws.push(Receipt(msg.sender, _amount));
        userWithdrawsIndex[msg.sender].push(withdraws.length - 1);
        emit CrabQueued(msg.sender, _amount, crabBalance[msg.sender], withdraws.length - 1);
    }
```

CrabNetting.sol#L319-L346
```solidity
    /**
     * @notice withdraw Crab from queue
     * @param _amount Crab amount to dequeue
     */
    function dequeueCrab(uint256 _amount) external {
        require(!isAuctionLive, "auction is live");
        crabBalance[msg.sender] = crabBalance[msg.sender] - _amount;
        require(
            crabBalance[msg.sender] >= minCrabAmount || crabBalance[msg.sender] == 0,
            "remaining amount smaller than minimum, consider removing full balance"
        );
        // deQueue crab from the last, last in first out
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userWithdrawsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = withdraws[userWithdrawsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete withdraws[userWithdrawsIndex[msg.sender][i - 1]];
            }
        }
        IERC20(crab).transfer(msg.sender, _amount);
        emit CrabDeQueued(msg.sender, _amount, crabBalance[msg.sender]);
    }
```

## Vulnerability Detail
Once the `deposits` and `withdraws` arrays exceed a somewhat reasonable number at around 500 in length, view functions will no longer work that access the variables because they _loop_ over every one of them - using an enormous amount of gas that leads to reverting.

CrabNetting.sol#L421-L432
```solidity
    /**
     * @return sum usdc amount in queue
     */
    function depositsQueued() external view returns (uint256) {
        uint256 j = depositsIndex;
        uint256 sum;
        while (j < deposits.length) {
            sum = sum + deposits[j].amount;
            j++;
        }
        return sum;
    }
```

CrabNetting.sol#L434-L445
```solidity
    /**
     * @return sum crab amount in queue
     */
    function withdrawsQueued() external view returns (uint256) {
        uint256 j = withdrawsIndex;
        uint256 sum;
        while (j < withdraws.length) {
            sum = sum + withdraws[j].amount;
            j++;
        }
        return sum;
    }
```

## Impact
After a few hundred users deposit or withdraw `USDC` and `Crab` a couple view functions will no longer be usable by outside sources. This means that the protocol cannot tell now or in the future the amount of funds being tossed around in the individual arrays, potentially losing value because of it.
## Code Snippet
https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L421-L449
## Tool used

Manual Review

## Recommendation

Instead of pushing and deleting from arrays, use an additional variable to keep track of the `Crab` and `USDC` amount in queue. This will prevent the need to do the expensive operation of looping every time.