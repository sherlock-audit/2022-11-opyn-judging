hyh

medium

# Griefing is possible for depositAuction and netAtPrice

## Summary

As active auction isn't a prerequisite for depositAuction() and netAtPrice(), both functions can be front-run with USDC withdrawal, making them fail asset amount check and revert.

## Vulnerability Detail

If netAtPrice() and depositAuction() be called when an auction is not live, a griefing attack is possible: Bob the depositor can front-run the functions, withdrawing the amount previously deposited, so there will be a deficit and the functions will be failing.

I.e. if an auction isn't started any depositor can manipulate these functions into unavailability. As the amounts need to be recalculated after Bob withdrew his funds this means proceeding with the action somewhat later, potentially in a different market conditions, which can be beneficial to Bob and serves as a reason for the attack. Say Bob can join, monitor and block the auctions with withdrawal based on some condition, having some kind of free option at the expense of other participants as a result.

## Impact

Manipulating netting and auction functions can be used for market timing and, when executed, generate profit for the attacker at the expense of other participants, who meet attackers' orders when it is suitable to him. Due to prerequisite of having `isAuctionLive == false` (say via auction script malfunction or an operational mistake) setting the severity to medium.

## Code Snippet

depositAuction() will fail on the router call if `_p.depositsQueued > IERC20(usdc).balanceOf(address(this))`:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L487-L536

```solidity
    /**
     * @dev takes in orders from mm's to buy sqth and deposits the usd amount from the depositQueue into crab along with the eth from selling sqth
     * @param _p DepositAuction Params that contain orders, usdToDeposit, uniswap min amount and fee
     */
    function depositAuction(DepositAuctionParams calldata _p) external onlyOwner {
        _checkOTCPrice(_p.clearingPrice, false);
        /**
         *     step 1: get eth from mm
         *     step 2: get eth from deposit usdc
         *     step 3: crab deposit
         *     step 4: flash deposit
         *     step 5: send sqth to mms
         *     step 6: send crab to depositors
         */
        uint256 initCrabBalance = IERC20(crab).balanceOf(address(this));
        uint256 initEthBalance = address(this).balance;

        uint256 sqthToSell = _debtToMint(_p.totalDeposit);
        // step 1 get all the eth in
        uint256 remainingToSell = sqthToSell;
        for (uint256 i = 0; i < _p.orders.length; i++) {
            ...
        }
        require(remainingToSell == 0, "not enough buy orders for sqth");

        // step 2
        ISwapRouter.ExactInputSingleParams memory params = ISwapRouter.ExactInputSingleParams({
            tokenIn: usdc,
            tokenOut: weth,
            fee: _p.ethUSDFee,
            recipient: address(this),
            deadline: block.timestamp,
            amountIn: _p.depositsQueued,
            amountOutMinimum: _p.minEth,
            sqrtPriceLimitX96: 0
        });
```

netAtPrice() will fail the check if `_quantity > IERC20(usdc).balanceOf(address(this))`:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L348-L357

```solidity
    /**
     * @dev swaps _quantity amount of usdc for crab at _price
     * @param _price price of crab in usdc
     * @param _quantity amount of USDC to net
     */
    function netAtPrice(uint256 _price, uint256 _quantity) external onlyOwner {
        _checkCrabPrice(_price);
        uint256 crabQuantity = (_quantity * 1e18) / _price;
        require(_quantity <= IERC20(usdc).balanceOf(address(this)), "Not enough deposits to net");
        require(crabQuantity <= IERC20(crab).balanceOf(address(this)), "Not enough withdrawals to net");
```


Notice that without the `require` checks above the functions will still be failing with deposit array out of bounds as loop is over total deposit amount only and when there is not enough deposit array length `k`/`i` go past it:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L568-L583

```solidity
        // step 6 send crab to depositors
        uint256 remainingDeposits = _p.depositsQueued;
        uint256 k = depositsIndex;

        to_send.crab = IERC20(crab).balanceOf(address(this)) - initCrabBalance;
        // get the balance between start and now
        to_send.eth = address(this).balance - initEthBalance;
        IWETH(weth).deposit{value: to_send.eth}();

        while (remainingDeposits > 0) {
            uint256 queuedAmount = deposits[k].amount;
            Portion memory portion;
            if (queuedAmount == 0) {
                k++;
                continue;
            }
```

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L359-L367

```solidity
        // process deposits and send crab
        uint256 i = depositsIndex;
        uint256 amountToSend;
        while (_quantity > 0) {
            Receipt memory deposit = deposits[i];
            if (deposit.amount == 0) {
                i++;
                continue;
            }
```

USDC withdrawals are open when auction isn't live:

https://github.com/sherlock-audit/2022-11-opyn/blob/main/crab-netting/src/CrabNetting.sol#L274-L304

```solidity
    /**
     * @notice withdraw USDC from queue
     * @param _amount USDC amount to dequeue
     */
    function withdrawUSDC(uint256 _amount) external {
        require(!isAuctionLive, "auction is live");

        usdBalance[msg.sender] = usdBalance[msg.sender] - _amount;
        require(
            usdBalance[msg.sender] >= minUSDCAmount || usdBalance[msg.sender] == 0,
            "remaining amount smaller than minimum, consider removing full balance"
        );

        // start withdrawing from the users last deposit
        uint256 toRemove = _amount;
        uint256 lastIndexP1 = userDepositsIndex[msg.sender].length;
        for (uint256 i = lastIndexP1; i > 0; i--) {
            Receipt storage r = deposits[userDepositsIndex[msg.sender][i - 1]];
            if (r.amount > toRemove) {
                r.amount -= toRemove;
                toRemove = 0;
                break;
            } else {
                toRemove -= r.amount;
                delete deposits[userDepositsIndex[msg.sender][i - 1]];
            }
        }
        IERC20(usdc).transfer(msg.sender, _amount);

        emit USDCDeQueued(msg.sender, _amount, usdBalance[msg.sender]);
    }
```
## Tool used

Manual Review

## Recommendation

Consider requiring that auction be started at the begin of netAtPrice() and both auction functions. While for auctions it's more like a formal check, the attack surface described  means netAtPrice() should be always run with `isAuctionLive == true` as well.

```solidity
+    require(isAuctionLive, "Auction needs to be started");  
```